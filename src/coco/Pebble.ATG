/******************************************************************************

Notes about ATG format:
= separates the sides of a production A = a b c .
. terminates a production A = a b c .
| separates alternatives a b | c | d e means a b or c or d e
( ) groups alternatives (a | b) c means a c or b c
[ ] option [a] b means a b or b
{ } iteration (0 or more times) {a} b means b or a b or a a b or ... 

******************************************************************************/

COMPILER Pebble

	const int // types
	  undef = 0, integer = 1, boolean = 2;

	const int // object kinds
	  var = 0, proc = 1;

	public CodeGenerator gen;
	public IExpr _headExpr;
	public ExecContext context;

	//------------------ token sets ------------------------------------
	/*
	static BitArray
	startOfTypeName = NewBitArray(_const, _volatile, _void, _char, _short, _int, _long,
		                _double, _signed, _unsigned, _struct, _union, _enum),
	startOfDecl     = NewBitArray(_typedef, _extern, _static, _auto, _register, _const, 
	                  _volatile, _void, _char, _short, _int, _long, _double, _signed, 
	                  _unsigned, _struct, _union, _enum);

	private static BitArray NewBitArray(params int[] val) {
		BitArray s = new BitArray(128);
		foreach (int x in val) s[x] = true;
		return s;
	}
	*/
	//---------- LL(1) conflict resolvers ------------------------------

	// Return the n-th token after the current lookahead token
	Token Peek (int n) {
		scanner.ResetPeek();
		Token x = la;
		while (n > 0) { x = scanner.Peek(); n--; }
		return x;
	}

	private bool IsTypeName(Token x) {
		return context.IsType(x.val);
	}

	bool IsDecl() { // return true if followed by Decl
		if ("override" == la.val 
				|| "void" == la.val
				|| "global" == la.val 
				|| "const" == la.val 
				|| "functype" == la.val 
				|| "funcdef" == la.val 
				|| "static" == la.val)
			return true;
		scanner.ResetPeek();
		Token laa = scanner.Peek();
		if (null == laa)
			return false;

		// Anywhere we have two identifiers in sequence, it must be a declaration.
		if (1 == la.kind && 1 == laa.kind)
			return true;

		// That isn't sufficient, though, in the case of templates, so here's the old check,
		// which also makes sure this isn't a ::.
		// Note this code is probably broken if there are ever static template members, ie. A<num>::x; 
		// I'm not positive that's needed. I suppose it depends on the order these productions are checked for.
		return IsTypeName(la) && null != laa && "::" != laa.val;
	}

	bool IsClassName() {
		return context.IsType(la.val);
	}

	bool IsScopeOpStart() {
		scanner.ResetPeek();
		Token laa = scanner.Peek();
		// Here I'm assuming type 1 means "identifier". That seems to be the case but unsure why.
		// This would not work for static in templates, btw.
		return 1 == la.kind && null != laa && "::" == laa.val;
	}

	// True, if the comma is not a trailing one, like the last one in: a, b, c,      
	bool NotFinalComma () {
		string peek = Peek(1).val;
		return la.val == "," && peek != "]" && peek != "}";
	}

/*****************************************************************************/
	CHARACTERS
/*****************************************************************************/
  letter = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz_".
  digit = "0123456789".
  cr  = '\r'.
  lf  = '\n'.
  tab = '\t'.
  CharInLine = ANY - '\r' - '\n'.
  NoDoubleQuoteNoBackslash = CharInLine - '\"' - '\\'.
  OctalDigit = "01234567".
  ZeroToThree = "0123".

/*****************************************************************************/
	TOKENS
/*****************************************************************************/
  ident  = letter {letter | digit}.

  //number = (digit {digit} '.' {digit}) | digit {digit}.
  /*
  number = ( 
				'.' digit {digit} [('e'|'E')  ['+'|'-'] digit {digit}]
				| digit {digit} '.' {digit} [('e'|'E')  ['+'|'-'] digit {digit}]
				| digit {digit} ('e'|'E')  ['+'|'-'] digit {digit}
			).
  */
	number = ( 
				'.' digit {digit} [('e'|'E')  ['+'|'-'] digit {digit}]
				| digit {digit} '.' {digit} [('e'|'E')  ['+'|'-'] digit {digit}]
				| digit {digit} ('e'|'E')  ['+'|'-'] digit {digit}
				| digit {digit}
			).
	
/*
  StringLiteral =
	'\"' [ ( NoDoubleQuoteNoBackslash |
	('\\' ( 'b' | 't' | 'n' | 'f' | 'r' | '\"' | '\'' | '\\' |
	OctalDigit | OctalDigit OctalDigit | ZeroToThree OctalDigit OctalDigit )) )
	{ ( NoDoubleQuoteNoBackslash |
	('\\' ( 'b' | 't' | 'n' | 'f' | 'r' | '\"' | '\'' | '\\' |
	OctalDigit | OctalDigit OctalDigit | ZeroToThree OctalDigit OctalDigit )) ) } ] '\"'.
*/
// Original above, below is Pebble's, which has fewer escape sequences.
StringLiteral =
	'\"' [ ( NoDoubleQuoteNoBackslash |
	('\\' ( 't' | 'n' | '\"' | '\\' )) )
	{ ( NoDoubleQuoteNoBackslash |
	('\\' ( 't' | 'n' | '\"' | '\\' )) ) } ] '\"'.

/*****************************************************************************/
/*	COMMENTS */
/*****************************************************************************/
COMMENTS FROM "/*" TO "*/" NESTED
COMMENTS FROM "//" TO lf

/*****************************************************************************/
/* WHITESPACE */
/*****************************************************************************/
IGNORE cr + lf + tab


/*****************************************************************************/
	PRODUCTIONS
/*****************************************************************************/

//---------------------------
//---------- Types ----------
//---------------------------

TypeSpecifier<ref ITypeRef type> 
=													(. bool isConst = false; .)										
	( FunctionType<ref type>
	| TypeSpecifierNoFunc<ref type>
    )												(. if (isConst) type.SetConst(true); .) .

// Use this for any type specifier BUT function types.
// Re: LL1 warning in TypeSpecifierNoFunc: "<" is start and successor of a deletable structure
//   This is because in this "new A < ", the parser isn't sure if this is "(new A) <" or the start of a template "new A<num>".
//   Because < is cannot operate on anything that can be produced by new, this is never an actual problem.
TypeSpecifierNoFunc<ref ITypeRef type> =			(. string className = null; ITypeRef genType = null; TypeRefList genericTypes = new TypeRefList(); .)
	 ident											(. className = t.val; .)
		['<' TypeSpecifier<ref genType>				(. genericTypes.Add(genType); .)
			{',' TypeSpecifier<ref genType>			(. genericTypes.Add(genType); .) }
		'>']										(. type = new TypeRef(className, genericTypes); .) .

FunctionTypeArg<ref TypeRefList argTypes, ref BoolList argHasDefaults>	(. ITypeRef argType = null; .)
=	TypeSpecifier<ref argType>						(. argTypes.Add(argType); .)
	[ '?'											(.	while (argHasDefaults.Count < argTypes.Count - 1)
															argHasDefaults.Add(false);
														argHasDefaults.Add(true); 
													.) 
	].

FunctionType<ref ITypeRef varType>					(. ITypeRef retType = null; TypeRefList args = new TypeRefList(); BoolList argHasDefaults = new BoolList(); bool isConst = false; .)	
=													
	"functype" 
	'<' (TypeSpecifier<ref retType> | "void"		(. retType = new TypeRef("void"); .) )
	'(' 
	[ FunctionTypeArg<ref args, ref argHasDefaults> 
		{ ','  FunctionTypeArg<ref args, ref argHasDefaults> }
	]
	')' '>'											(. varType = new TypeRef_Function(retType, args, argHasDefaults, false, isConst); .) .


//----------------------------------
//---------- Declarations ----------
//----------------------------------

FunctionDeclArg<ref TypeRefList argTypes, ref StrList argNames, ref LiteralList defaultValues>	(. ITypeRef argType = null; IExpr litValue = null; .)
=	TypeSpecifier<ref argType> ident				(. argTypes.Add(argType); argNames.Add(t.val); .)
	[ '=' Literal<ref litValue>						(.	while (defaultValues.Count < argTypes.Count)
															defaultValues.Add(null);
														defaultValues[defaultValues.Count - 1] = (Expr_Literal) litValue; .) 
	]. 

Decl<ref IExpr expr>									(. string sym = null; ITypeRef type = null; IExpr init = null; TypeRefList argTypes = new TypeRefList(); StrList argNames = new StrList(); LiteralList defaultValues = new LiteralList(); IExpr body = null; IExpr script = null; DeclMods mods = new DeclMods(); .)
=	{
		 "const"		(. mods._const = true; .)
		| "global"		(. mods._global = true; .)
		| "static"		(. mods._static = true; .)
		| "override"	(. mods._override = true; .)
	}
	( TypeSpecifier<ref type> | "void" (. type = new TypeRef("void"); .) )		(. if (null != type) type.SetConst(mods._const); .) // Unsure why I have to do the null check there, but compiler crashes on some syntax errors if I don't. (eg. "num const;")
	Ident<ref sym> 
	(	
		(. expr = new Expr_Set(this, type, sym, mods); .)
		[(
			'=' AssignExpr<ref init>					
				(.	((Expr_Set)expr).SetValue(init); 
					if (init is Expr_New && null == ((Expr_New)init).typeRef)
						((Expr_New)init).typeRef = type;
				.) 
		|
			"<-" Expr<ref script>						(. expr = new Expr_ScriptToValue(this, expr, script); .) 
		)] 
		';'
	|
		'(' 
		[
			FunctionDeclArg<ref argTypes, ref argNames, ref defaultValues>
			{ ',' FunctionDeclArg<ref argTypes, ref argNames, ref defaultValues> }
		]
		')' 
		EmbeddedStatementBlock<ref body>				(. expr = Expr_Set.CreateFunctionLiteral(this, type, sym, argTypes, defaultValues, argNames, body, mods); .) 
	).	


//---------------------------------
//---------- Expressions ----------
//---------------------------------

// This handles operator precedence.  Lowest precedence first, I'm pretty sure.

Expr<ref IExpr expr>       
=	AssignExpr<ref expr>.

AssignExpr<ref IExpr expr>		(. IExpr expr1 = null, expr2 = null; .)
=	CondExpr<ref expr1>			(. expr = expr1; .)
	[( '=' Expr<ref expr2>		(. expr = new Expr_Assign(this, expr1, expr2); .) 
	 | "+=" Expr<ref expr2>		(. expr = new Expr_Assign(this, expr1, new Expr_BinOp(this, Expr_BinOp.OP.ADD, expr1, expr2)); .) 
	 | "-=" Expr<ref expr2>		(. expr = new Expr_Assign(this, expr1, new Expr_BinOp(this, Expr_BinOp.OP.SUB, expr1, expr2)); .) 
	 | "*=" Expr<ref expr2>		(. expr = new Expr_Assign(this, expr1, new Expr_BinOp(this, Expr_BinOp.OP.MULT, expr1, expr2)); .) 
	 | "/=" Expr<ref expr2>		(. expr = new Expr_Assign(this, expr1, new Expr_BinOp(this, Expr_BinOp.OP.DIV, expr1, expr2)); .) 
	 | "..=" Expr<ref expr2>	(. expr = new Expr_Assign(this, expr1, new Expr_BinOp(this, Expr_BinOp.OP.CONCAT, expr1, expr2)); .)
	 | "<<" Expr<ref expr2>		(. expr = new Expr_Stream(this, expr1, expr2); .) 
	)].

CondExpr<ref IExpr expr>								(. IExpr expr1 = null, expr2 = null, expr3 = null; .)
=	LogOrExpr<ref expr1>								(. expr = expr1; .)
	['?' Expr<ref expr2> ':' CondExpr<ref expr3>		(. expr = new Expr_Conditional(this, expr1, expr2, expr3); .) ].

LogOrExpr<ref IExpr expr>					(. IExpr expr1 = null, expr2 = null; .)
=	LogAndExpr<ref expr1>					(. expr = expr1; .)
	{"||" LogAndExpr<ref expr2>				(. expr = new Expr_BinOp(this, Expr_BinOp.OP.OR, expr, expr2); .) }.

LogAndExpr<ref IExpr expr>					(. IExpr expr1 = null, expr2 = null; .)
=	EqlExpr<ref expr1>						(. expr = expr1; .)
	{"&&" EqlExpr<ref expr2>				(. expr = new Expr_BinOp(this, Expr_BinOp.OP.AND, expr, expr2); .) }.

EqlExpr<ref IExpr expr>						(. IExpr expr1 = null, expr2 = null; .)
=	RelExpr<ref expr1>						(. expr = expr1; .)
	{("==" RelExpr<ref expr2>				(. expr = new Expr_Compare(this, expr, expr2, false); .)
	 |"!=" RelExpr<ref expr2>				(. expr = new Expr_Compare(this, expr, expr2, true);  .)
	 |"<-" RelExpr<ref expr2>				(. expr = new Expr_ScriptToValue(this, expr, expr2);	.)
	 )}.

RelExpr<ref IExpr expr>						(. IExpr expr1 = null, expr2 = null; .)
=	AddExpr<ref expr1>						(. expr = expr1; .)
	{('<' AddExpr<ref expr2>				(. expr = new Expr_BinOp(this, Expr_BinOp.OP.LT, expr, expr2); .)
	 |'>' AddExpr<ref expr2>				(. expr = new Expr_BinOp(this, Expr_BinOp.OP.GT, expr, expr2); .)
	 |"<=" AddExpr<ref expr2>				(. expr = new Expr_BinOp(this, Expr_BinOp.OP.LEQ, expr, expr2); .)
	 |">=" AddExpr<ref expr2>				(. expr = new Expr_BinOp(this, Expr_BinOp.OP.GEQ, expr, expr2); .)
	 |"~=" AddExpr<ref expr2>				(. expr = new Expr_BinOp(this, Expr_BinOp.OP.STREQI, expr, expr2); .)
	 )}.

AddExpr<ref IExpr expr>						(. IExpr expr1 = null, expr2 = null; .)
=	MultExpr<ref expr1>						(. expr = expr1; .)
	{('+' MultExpr<ref expr2>				(. expr = new Expr_BinOp(this, Expr_BinOp.OP.ADD, expr, expr2); .)
	 |'-' MultExpr<ref expr2>				(. expr = new Expr_BinOp(this, Expr_BinOp.OP.SUB, expr, expr2); .)
	 |".." MultExpr<ref expr2>				(. expr = new Expr_BinOp(this, Expr_BinOp.OP.CONCAT, expr, expr2); .)
	 )}.

MultExpr<ref IExpr expr>					(. IExpr expr1 = null, expr2 = null; .)
=	CastExpr<ref expr1>						(. expr = expr1; .)
	{('*' CastExpr<ref expr2>				(. expr = new Expr_BinOp(this, Expr_BinOp.OP.MULT, expr, expr2); .)
	 |'/' CastExpr<ref expr2>				(. expr = new Expr_BinOp(this, Expr_BinOp.OP.DIV, expr, expr2); .)
	 |'%' CastExpr<ref expr2>				(. expr = new Expr_BinOp(this, Expr_BinOp.OP.MOD, expr, expr2); .)
	 | "**" CastExpr<ref expr2>				(. expr = new Expr_BinOp(this, Expr_BinOp.OP.POW, expr, expr2); .)
	 )}.

CastExpr<ref IExpr expr>   
= 	UnaryExpr<ref expr>.

UnaryExpr<ref IExpr expr>					(. List<int> li = new List<int>(); .)
=	{("++"									(. li.Add(0); .)
	| "--"									(. li.Add(1); .)
	| '#'									(. li.Add(2); .)
	| '$'									(. li.Add(3); .)
	)}					
	UnaryPost<ref expr>						(.	for (int i = li.Count - 1; i >= 0; --i) {
													if (0 == li[i])
														expr = Expr_Assign.CreateInc(this, expr); 
													else if (1 == li[i])
														expr = Expr_Assign.CreateDec(this, expr); 
													else if (2 == li[i])
														expr = new Expr_Length(this, expr); 
													else if (3 == li[i])
														expr = new Expr_UnOp(this, Expr_UnOp.OP.TOSTRING, expr);	
												}
	.) .


UnaryPost<ref IExpr expr>						(. .)
=	( PostfixExpr<ref expr>
	| '+' CastExpr<ref expr>					(. expr = new Expr_UnOp(this, Expr_UnOp.OP.POS, expr); .)
	| '-' CastExpr<ref expr>					(. expr = new Expr_UnOp(this, Expr_UnOp.OP.NEG, expr); .)
	| '!' CastExpr<ref expr>					(. expr = new Expr_UnOp(this, Expr_UnOp.OP.NOT, expr); .)
	).

PostfixExpr<ref IExpr expr>					(. ExprList args = null; IExpr indexExpr = null; .)
=	Primary<ref expr>
	{ '[' Expr<ref indexExpr> ']'			(. expr = new Expr_Index(this, expr, indexExpr); .)
	| '.'  ident							(. expr = new Expr_Dot(this, expr, t.val); .)
	| '(' [ArgExprList<ref args>] ')'		(. expr = new Expr_Call(this, expr, args); args = null; .) /* Unsure why, but without setting args to null there there is some sort of recursion problem and args appear in function calls they shouldn't. */
	| "++"									(. expr = new Expr_Postrement(this, expr, false); .)
	| "--"									(. expr = new Expr_Postrement(this, expr, true); .)
	}.

//Primary = ident | intcon | floatcon | charcon | string | '(' Expr ')'.
Primary<ref IExpr expr>										(. string className = null; IExpr exprBlock = null; ITypeRef valType = null; IExpr initializer = null; .)
=	( 
	(IF(IsScopeOpStart()) Ident<ref className> "::" ident	(. expr = new Expr_Scope(this, className, t.val); .) )
	| "::" ident											(. expr = new Expr_Scope(this, null, t.val); .)
	| ident													(. expr = new Expr_Symbol(this, t.val); .)
	| "this"												(. expr = new Expr_This(this); .)
	| "catch" EmbeddedStatementBlock<ref exprBlock> 		(. expr = new Expr_Catch(this, exprBlock); .)
	//| Test<ref expr>
	| Literal<ref expr>
	| '(' Expr<ref expr> ')'
	| "new" [TypeSpecifierNoFunc<ref valType>] [ EmbeddedStatementBlock<ref initializer> ] (. expr = new Expr_New(this, valType, initializer); .)
	).

Literal<ref IExpr expr>
=	( "true"								(. expr = new Expr_Literal(this, true, IntrinsicTypeDefs.BOOL); .)
	| "false"								(. expr = new Expr_Literal(this, false, IntrinsicTypeDefs.BOOL); .)
	| "null"								(. expr = new Expr_Literal(this, null, IntrinsicTypeDefs.NULL); .)
	| number								(. expr = new Expr_Literal(this, Convert.ToDouble(t.val), IntrinsicTypeDefs.NUMBER); .)
	| StringLiteral							(. expr = new Expr_Literal(this, t.val.Substring(1, t.val.Length - 2), IntrinsicTypeDefs.STRING); .)
	).

// This is useful for productions that require two strings, so they can't both use t.val.
Ident<ref string id>
=	ident				(. id = t.val; .) .


// This is exactly the original, strangely.
// I think it is saying "Constant expressions are any expressions that aren't assignments"?
// ConstExpr<ref IExpr expr> = CondExpr<ref expr>.

ArgExprList<ref ExprList args>		(. args = new ExprList(); IExpr arg1 = null, arg2 = null; .)
=	AssignExpr<ref arg1>			(. args.Add(arg1); .)
	{','  AssignExpr<ref arg2>		(. args.Add(arg2); .) }.

ForExpr<ref IExpr expr>			(. string sym = null; IExpr minExpr = null; IExpr maxExpr = null; IExpr body = null; IExpr stepExpr = null; .)
=	"for" '(' Ident<ref sym> '=' Expr<ref minExpr> ',' Expr<ref maxExpr> [ ',' Expr<ref stepExpr> ]	')' 
		ForOrIfStat<ref body>							(. expr = new Expr_For(this, sym, minExpr, maxExpr, stepExpr, body); .) .


ForEachExpr<ref IExpr expr>		(. string kIdent = null, vIdent = null; IExpr sym = null, body = null; .)
=	"foreach" '(' Ident<ref kIdent> ',' Ident<ref vIdent> "in" Expr<ref sym> ')' 
		ForOrIfStat<ref body>							(. Expr_ForEach forEachExpr = new Expr_ForEach(this, sym, kIdent, vIdent, body); expr = forEachExpr; .) .

TypeAliasStat<ref IExpr expr>	(. ITypeRef typeRef = null; string ident = null; .)
=	"typealias" Ident<ref ident> '=' TypeSpecifier<ref typeRef>	(. Expr_TypeAlias taExpr = new Expr_TypeAlias(this, ident, typeRef); expr = taExpr; taExpr.PreRegister(context); .) .


Class<ref IExpr expr>							(. Expr_Class scope = null; bool isSealed = false; bool isUninstantiable = false; IExpr memberDec = null; IExpr block = null; .)
=	{ "sealed"									(. isSealed = true; .)
	 |"uninstantiable"							(. isUninstantiable = true; .) 
	}
	"class" ident								(. scope = new Expr_Class(this, t.val); expr = scope; scope.isSealed = isSealed; scope.isUninstantiable = isUninstantiable; .)
		[ ':' ident								(. scope.parent = t.val; .) ]
		[ '{' { 
			Decl<ref memberDec>							(. scope.AddMember((Expr_Set) memberDec); .) 
			| "constructor" EmbeddedStatementBlock<ref block>	(. scope.SetConstructor(context, block); .)
		} '}' ]
	';' .


Enum<ref IExpr expr>							(. IExpr initializer = null; Expr_Enum e = null; ITypeRef enumType = null; string exprName; string valName = null;.)
=	"enum" '<' TypeSpecifier<ref enumType> '>' ident  (. exprName = t.val; e = new Expr_Enum(this, exprName, enumType); expr = e; .)
	'{' [
		Ident<ref valName> [ '=' Expr<ref initializer> ]								(. e.AddValue(valName, initializer); initializer = null; .) 
		{
			IF (NotFinalComma()) "," Ident<ref valName> [ '=' Expr<ref initializer> ] 	(. e.AddValue(valName, initializer); initializer = null; .)
		} ] [","]
	"}" .

// Note that the assert block is a StatBlock, not EmbeddedStatementBlock. This is so we can have assert unit tests with class declarations in them.
Assert<ref IExpr expr>		(. IExpr conditionExpr = null; IExpr messageExpr = null; IExpr block = null; .)
=	"assert" '(' Expr<ref conditionExpr> {',' Expr<ref messageExpr> } ')' ( StatBlock<ref block> | ';' )
	(. 
#if PEBBLE_ASSERTOFF
		expr = new Expr_Literal(this, true, IntrinsicTypeDefs.BOOL);
#else
		expr = new Expr_Assert(this, conditionExpr, messageExpr, block, true);
#endif
	.) .

//--------------------------------
//---------- Statements ----------
//--------------------------------

// This is the very top level. Can be most things, namely class declarations, though not control expressions.
Stat<ref IExpr expr>											(. Expr_If ifExpr = null; IExpr cond = null; IExpr trueCase = null; IExpr falseCase = null; .)
=	StatBlock<ref expr>
	| Class<ref expr>
	| Enum<ref expr>
	| "if" '(' Expr<ref cond> ')' ForOrIfStat<ref trueCase>		(. ifExpr = new Expr_If(this, cond, trueCase); expr = ifExpr; .)
		["else" ForOrIfStat<ref falseCase> 						(. ifExpr.falseCase = falseCase; .) ]
	| ForExpr<ref expr>
	| ForEachExpr<ref expr>
	| Assert<ref expr>
	| TypeAliasStat<ref expr> ';'
	| (IF(IsDecl()) Decl<ref expr> | Expr<ref expr> ';' )
	| ';'
	.

// A block of stats. This is only used for blocks just below the top level.
//! What is it's value if it has no Statements, though?!
StatBlock<ref IExpr exprBlock>
=						(.	Expr_ExprList block = new Expr_ExprList(this);
							exprBlock = block;
							IExpr expr = null; 
						.)
	'{'	{
		Stat<ref expr>	(. if (null != expr) block.nodes.Add(expr); expr = null; .) 
	} '}'.

// This is the type of statement that could be, for example, the body of a for or if statement.
// Re: LL1 warning in EmbeddedStat: "else" is start and successor of deletable structure
//   This warning is because in this expression "if (b) if (c) else d;" it is ambiguous which if the else belongs to. 
//   It doesn't matter: whichever Coco chooses, it should be consistent, and besides anyone who codes like this is playing with fire anyway.
EmbeddedStat<ref IExpr expr>									(. Expr_If ifExpr = null; IExpr cond = null; IExpr trueCase = null; IExpr falseCase = null; .)
=	( EmbeddedStatementBlock<ref expr>
	| (IF(IsDecl()) Decl<ref expr> | Expr<ref expr> ';' )
	| "if" '(' Expr<ref cond> ')' ForOrIfStat<ref trueCase>		(. ifExpr = new Expr_If(this, cond, trueCase); expr = ifExpr; .)
		["else" ForOrIfStat<ref falseCase> 						(. ifExpr.falseCase = falseCase; .) ] 
	| ForExpr<ref expr>
	| ForEachExpr<ref expr>
	| Assert<ref expr>
	| "break" ';'												(. expr = new Expr_Break(this); .)
	| "continue" ';'											(. expr = new Expr_Continue(this); .)
	| "return" [ Expr<ref cond> ] ';'							(. expr = new Expr_Return(this, cond); .)
	| ';'
	).

// A series of EMBEDDED Statements.  Has value: the value of the final Statement.
// What is it's value if it has no Statements, though?!
EmbeddedStatementBlock<ref IExpr exprBlock>
=						(.	Expr_ExprList block = new Expr_ExprList(this);
							exprBlock = block;
							IExpr expr = null; 
						.)
	'{'	{
		EmbeddedStat<ref expr>	(. if (null != expr) block.nodes.Add(expr); expr = null; .) 
	} '}'.

/* This is used ONLY for the statements immediately following If or For statements. Those statements can be anything that could be inside
a If or For BLOCK, EXCEPT declarations and asserts.
*/
ForOrIfStat<ref IExpr expr>										(. Expr_If ifExpr = null; IExpr cond = null; IExpr trueCase = null; IExpr falseCase = null; .)
=	( EmbeddedStatementBlock<ref expr>
	| Expr<ref expr> ';'
	| "if" '(' Expr<ref cond> ')' EmbeddedStat<ref trueCase>	(. ifExpr = new Expr_If(this, cond, trueCase); expr = ifExpr; .)
		["else" EmbeddedStat<ref falseCase> 					(. ifExpr.falseCase = falseCase; .) ] 
	| ForExpr<ref expr>
	| ForEachExpr<ref expr>
	| "break" ';'												(. expr = new Expr_Break(this); .)
	| "continue" ';'											(. expr = new Expr_Continue(this); .)
	| "return" [ Expr<ref cond> ] ';'							(. expr = new Expr_Return(this, cond); .)
	| ';'
	).

//----------------------------
//---------- Script ----------
//----------------------------
	
// Input is a series of statements.
// Empty string is a valid Pebble script (it results in no expressions), which I believe is why Pebble is deletable.
Pebble
=							(. Expr_ExprList list = null; IExpr _nextExpr = null; .)
	{ Stat<ref _nextExpr>	(.
								if (null != _nextExpr) {
									if (null == _headExpr)
										_headExpr = _nextExpr;
									else {
										if (null == list) {
											list = new Expr_ExprList(this);
											list.createScope = false;
											list.nodes.Add(_headExpr);
											_headExpr = list;
										}
										list.nodes.Add(_nextExpr);
									}
									_nextExpr = null;
								}
							.) 
	}.


END Pebble.

